//=============================================================================
// ■ Audio_PlaySound
//-----------------------------------------------------------------------------
//	音量とピッチの変更サンプルです。
//	ウィンドウ内をクリックすると音量とピッチを変更します。
//=============================================================================

#include "lumino.as"

	sound = 0
	
	screen 0, 300, 300, 1

	LNConfig_SetDirectMusicMode LN_DIRECTMUSICMODE_THREAD_REQUEST
	LNConfig_SetUserWindowHandle hwnd

	LNEngine_InitializeAudio

	color 128, 128, 128
	line -1, 150, 300, 150
	line 150, -1, 150, 300
	font "", 12
	pos 2, 138	 : mes "volume 0%"
	pos 232, 138 : mes "volume 100%"
	pos 100, 2	 : mes "pitch 50%"
	pos 152, 286 : mes "pitch 150%"

	gosub *load

*main

	stick key, 256
	if key & 256 {
		pos mousex - 2, mousey - 6
		mes "*"
		LNSound_SetVolume sound, double(mousex) / 300
		LNSound_SetPitch  sound, double(mousey) / 300 + 0.5
	}

	if key & 512 {
		gosub *load
	}
	
	await 32
	goto *main

*load

	dialog "wav;*.ogg;*.mp3;*.mid", 16
	if stat == 0 : end

	LNSound_Create refstr, sound
	LNSound_SetLoopEnabled sound, LN_TRUE
	LNSound_Play sound

	return

//=============================================================================
// ■ Audio_SoundPlayer
//-----------------------------------------------------------------------------
//	簡単なサウンドプレイヤーのサンプルです。
//=============================================================================

#include "lumino.as"

	sound = 0

	screen 0, 320, 160
	
	LNConfig_SetDirectMusicMode LN_DIRECTMUSICMODE_THREAD_REQUEST
	LNConfig_SetUserWindowHandle hwnd
	
	LNEngine_InitializeAudio
	if stat != LN_OK : end

	dialog "wav;*.ogg;*.mp3;*.mid", 16
	if stat == 0 : end

	LNSound_Create refstr, sound
	
	objsize 96, 24
	button "再生", *sound_play
	button "停止", *sound_stop
	button "一時停止", *sound_pause_on
	button "一時停止解除", *sound_pause_off

	pos 100, 0
	button "フェードイン", *sound_fadein
	button "フェードアウト", *sound_fadeout

*main
	
	redraw 0

	color 255, 255, 255
	boxf
	color 128, 128, 128

	pos 100, 64
	
	// 再生状態
	LNSound_GetPlayingState sound, state
	switch stat
		case LN_SOUNDPLAYINGSTATE_STOPPED
			mes "停止中"
			swbreak
		case LN_SOUNDPLAYINGSTATE_PLAYING
			mes "再生中"
			swbreak
		case LN_SOUNDPLAYINGSTATE_PAUSING
			mes "一時停止中"
			swbreak
	swend
	
	// 再生サンプル数
	LNSound_GetPlayedSamples sound, samples
	mes "Played : " + samples
	
	// トータルサンプル数
	LNSound_GetTotalSamples sound, total
	mes "Total  : " + total

	redraw 1
	await 32
	goto *main

*sound_play
	LNSound_Play sound
	goto *main
	
*sound_stop
	LNSound_Stop sound
	goto *main
	
*sound_pause_on
	LNSound_Pause sound
	goto *main
	
*sound_pause_off
	LNSound_Resume sound
	goto *main
	
*sound_fadein
	LNSound_SetVolume sound, 0
	LNSound_Play sound
	
	// 10 秒かけてフェードイン
	LNSound_FadeVolume sound, 100, 10.0, LN_SOUNDFADEBEHAVIORBEHAVIOR_CONTINUE	
	goto *main
	
*sound_fadeout
	// 10 秒かけてフェードアウト → 停止
	LNSound_FadeVolume sound, 0, 10.0, LN_SOUNDFADEBEHAVIOR_STOP_RESET	
	goto *main
